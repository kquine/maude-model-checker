
mod CLIENT-SERVER is
    protecting NAT .
    sorts Oid Nat? Cnts Conf .
    subsort Nat < Nat? .
    subsort Cnts < Conf .

    op nil : -> Nat? [ctor] .
    ops a b c d e f g : -> Oid [ctor] .
    op f : Oid Oid Nat -> Nat [ctor] .
    op `{_`,_`} : Oid Nat -> Cnts [ctor] .

    op _<-_ : Oid Cnts -> Conf [ctor] .
    op `[_`] : Oid -> Conf [ctor] .
    op `[_`,_`,_`,_`] : Oid Oid Nat Nat? -> Conf [ctor] .

    op null : -> Conf [ctor] .
    op __ : Conf Conf -> Conf [ctor assoc comm id: null] .

    vars C S I : Oid .  vars N M : Nat .
    var W : Nat? .  var CNT : Cnts . 

    rl [req] : [C,S,N,nil] => [C,S,N,nil] (S <- {C,N}) [metadata "req(C)"] .
    rl [reply] : (S <- {C,N}) [S] => [S] (C <- {S,f(S,C,N)}) [metadata "fair(S,C)"] .
    rl [rec] : (C <- {S,M}) [C,S,N,W] => [C,S,N,M] [metadata "fair(C)"] .
    rl [dupl] : (I <- CNT) => (I <- CNT) (I <- CNT)  .
    rl [loss] : I <- CNT => null  .
endm

load ltlr-checker

mod CLIENT-SERVER-CHECK is
    protecting CLIENT-SERVER .
    inc LTLR-MODEL-CHECKER .

    subsort Conf < State .

    var X : Conf .  vars A B : Action . var CNT : Cnts . 
    vars C S I : Oid .  vars M N : Nat . var W : Nat? .

    op answered : Oid -> Prop .
    eq X [C, S, N, M]   |= answered(C) = true .

    op init : -> State .
    eq init = [a] [b,a,1,nil] [c,a,0,nil] .

    *** abstraction
    eq (I <- CNT) (I <- CNT) = (I <- CNT) .
    *** ---extra rules below added by coherence completion
    rl [reply]: (S <-{C,N}) [S]         => (S <-{C,N}) [S] (C <-{S,f(S,C,N)}) .
    rl [rec]  : (C <-{S,M}) [C,S,N,nil] => (C <-{S,M}) [C,S,N,M] .
    rl [loss] : (I <-{C,N})             => (I <-{C,N}) .
endm

load ltlr-interface

---set show gc on .
set verbose on .

--- false
(mc in CLIENT-SERVER-CHECK : init |= <> answered(b) .)

--- true
(pfmc in CLIENT-SERVER-CHECK : init |= <> answered(b) .)

--- true
(pfmc in CLIENT-SERVER-CHECK : init |= <> answered(b) under
  just(req(b)) .)


--- false
(mc in CLIENT-SERVER-CHECK : init |= <> answered(b) under 
   just(req(b)) ; fair(rec(b))  .)

--- true
(mc in CLIENT-SERVER-CHECK : init |= <> answered(b) under 
   just(req(b)) ; fair(rec(b)) ; fair(reply(a,b)) .)

--- false
(mc in CLIENT-SERVER-CHECK : init |= <> answered(b) under 
   just(req(b)) ; just(rec(b)) ; just(reply(a,b)) .)

--- false
(mc in CLIENT-SERVER-CHECK : init |= <> answered(b) under
  fair(rec(C:Oid)) ; fair(reply(S:Oid,C:Oid)) .)

--- true 
(mc in CLIENT-SERVER-CHECK : init |= <> answered(b) under
  just(req(C:Oid)) ; fair(rec(C:Oid)) ; fair(reply(S:Oid,C:Oid)) .)

