load symbolic-checker

--- Lamport's Bakery protocol in Maude.
--- From: All About Maude - A High-Performance Logical Framework. 
(fmod BAKERY-SYNTAX is 
  sort Name .
  op 0 : -> Name [ctor] .
  op s : -> Name [ctor] .
  op __ : Name Name -> Name [ctor comm assoc id: 0] .

  sorts ModeIdle ModeWait ModeCrit Mode . 
  subsorts ModeIdle ModeWait ModeCrit < Mode . 
  sorts ProcIdle ProcWait Proc ProcIdleSet ProcWaitSet ProcSet . 
  subsorts ProcIdle < ProcIdleSet . 
  subsorts ProcWait < ProcWaitSet . 
  subsorts ProcIdle ProcWait < Proc < ProcSet . 
  subsorts ProcIdleSet < ProcWaitSet < ProcSet . 
  
  op idle : -> ModeIdle . 
  op wait : Name -> ModeWait . 
  op crit : Name -> ModeCrit . 
  op `[_`,_`] : Name ModeIdle -> ProcIdle . 
  op `[_`,_`] : Name ModeWait -> ProcWait . 
  op `[_`,_`] : Name Mode -> Proc . 
  op none : -> ProcIdleSet . 
  op __ : ProcIdleSet ProcIdleSet -> ProcIdleSet [assoc comm id: none] . 
  op __ : ProcWaitSet ProcWaitSet -> ProcWaitSet [assoc comm id: none] .
  op __ : ProcSet ProcSet -> ProcSet [assoc comm id: none] .

  sort Conf . 
  op _;_;_ : Name Name ProcSet -> Conf .
endfm)

(mod BAKERY is
  protecting BAKERY-SYNTAX . 
  
  var PS : ProcSet .  vars N M K : Name . 
  
  rl [wake] : N ; M ; [K, idle] PS => s N ; M ; [K, wait(N)] PS .
  rl [crit] : N ; M ; [K, wait(M)] PS => N ; M ; [K, crit(M)] PS .
  rl [exit] : N ; M ; [K, crit(M)] PS => N ; s M ; [K, idle] PS .
endm)

--- prop definitions for bakery
(mod BAKERY-SATISFACTION1 is
  pr BAKERY .
  pr SYMBOLIC-CHECKER .

  subsort Conf < State .

  ops ever-wait? ever-crit? : -> Prop . 

  vars N M : Name . vars PS : ProcSet . 
  
  eq s N ; M ; PS |= ever-wait? = true [variant] . 
  eq 0 ; M ; PS    |= ever-wait? = false [variant] . 
  eq N ; s M ; PS |= ever-crit? = true [variant] .
  eq N ; 0 ; PS    |= ever-crit? = false [variant] . 
endm)

--- set verbose on .

---(

(lfmc N ; N ; [0, idle] [s, idle] |= [] (ever-wait? -> <> ever-crit?) .)

(lfmc N ; N ; [0, idle] [s, idle] |= [] (ever-wait? -> ever-crit?) .)

*** deadlock counterexample
(lfmc N ; M ; IS:ProcIdleSet |= [] (ever-wait? -> <> ever-crit?) .)

*** spurious counterexample
(lfmc N ; M ; WS:ProcWaitSet |= [] (ever-wait? -> <> ever-crit?) .)
---)

(mod BAKERY-SATISFACTION2 is
  pr BAKERY .
  pr SYMBOLIC-CHECKER .

  subsort Conf < State .

  ops ex? ini : -> Prop . 

  var WS : ProcWaitSet . var IS : ProcIdleSet . var PS : ProcSet . 
  vars N M K M1 M2 K1 K2 : Name .

--- NOTE: ProcIdleSet < ProcWaitSet
  eq N ; M ; WS |= ex? = true [variant] .
  eq N ; M ; [K1, crit(M1)] WS |= ex? = true [variant] .
  eq N ; M ; [K1, crit(M1)] [K2, crit(M2)] PS |= ex? = false [variant] .
endm)

---(
--- no counterexample
(lmc [10] N:Name ; N:Name ; [0, idle] [s, idle] |= [] ex? .)

--- true
(lfmc N:Name ; N:Name ; [0, idle] [s, idle] |= [] ex? .)

--- false
(lfmc N ; M ; WS |= [] ex?  .)

--- false (infinite graph, but a counterexample exists)
(lfmc N ; N ; WS |= [] ex?  .)

***( true, but non terminating
(lfmc N ; N ; IS |= [] ex?  .)
***)
---)

(mod BAKERY-SATISFACTION2-ABS is
  pr BAKERY-SATISFACTION2 .

  var PS : ProcSet .  vars N M L K1 K2 K3 K4 : Name . var IS : ProcIdleSet .

  eq s s s N M ; M ; IS [K1, wait(N M)] [K2, wait(s N M)] [K3, wait(s s N M)]
   =   s s N M ; M ; IS [K1, wait(N M)] [K2, wait(s N M)]
  [variant] .

  eq s s s N M ; M ; IS [K4, crit(L)] [K1, wait(N M)] [K2, wait(s N M)] [K3, wait(s s N M)]
   =   s s N M ; M ; IS [K4, crit(L)] [K1, wait(N M)] [K2, wait(s N M)]
  [variant] .

endm)

(lfmc N ; N ; IS |= [] ex?  .)

